plugins {
    id 'java'
}

group = 'net.minheur.potoflux_card_learning'
version = '2.4'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // PotoFlux
    implementation name: 'PotoFlux-6.5'

    // PotoFlux dependencies
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation "org.reflections:reflections:0.10.2"

    // theme
    implementation 'com.formdev:flatlaf:3.3'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.minheur.potoflux.PotoFlux',
                'Implementation-Version': project.version
        )
    }
}

processResources {
    filesMatching("modVersion.properties") {
        expand "version": version
    }
}

tasks.register("generateVersionProperties") {
    def outputDir = file("$buildDir/generated/resources")
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        def file = new File(outputDir, "modVersion.properties")
        file.text = "version=${project.version}"
    }
}

sourceSets.main.resources.srcDir("$buildDir/generated/resources")

tasks.processResources.dependsOn(generateVersionProperties);

test {
    useJUnitPlatform()
}

tasks.register("run", JavaExec) {
    group = "potoflux"
    description = "Run PotoFlux"

    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("net.minheur.potoflux.PotoFlux")

    args("devEnv")
}